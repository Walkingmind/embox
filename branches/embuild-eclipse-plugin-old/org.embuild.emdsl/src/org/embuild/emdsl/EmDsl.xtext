grammar org.embuild.emdsl.EmDsl with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate emDsl "http://www.embuild.org/emdsl/EmDsl"

Model:
	Package?
	(units+=Unit)*;

terminal IGNORE:
	'#/##'->'##/#';

terminal SL_COMMENT:
	'#' !('\n' | '\r')* ('\r'? '\n')?;

terminal ML_COMMENT:
	SL_COMMENT+;

Filename:
	(ID | '-' | '.')+;

FilenameWithWildcards:
	(Filename
	| '?' | '*'
	| '[' Filename ('|' Filename)* ']')+;

TextWithoutParens:
	(ID | INT
	| '+' | '-' | '*' | '/' | '='
	| '.' | '|' | '%' | ':' | ';' | '@' | ','
	| '"' | "'" | '`' | '&' | '\\' | '!' | '?' | '$'
	| '(' | ')'
	| '<' | '>'
	| '[' | ']'
	| '{' | '}' | 'SUBDIRS' | STRING)+;

Text:
	(TextWithoutParens)+;

ContLine:
	'\\\n' | '\\\r\n';

MakeText:
	Text;

Unit:
	Ifdef | IGNORE | Subdir | EntityDef | PropAssign | Flags;

Ifdef:
	('ifdef' | 'ifndef') condition=ID
	(units+=Unit)*
	'endif';

EntityDef:
	ModsDef | ApisDef | LibsDef;

Package:
	'$_PACKAGE' Assignment name=QualifiedName;

ApiQN:
	QualifiedName;

Api:
	name=ApiQN;

ApisDef:
	'$_APIS' Assignment (apis+=Api)+;

LibQN:
	Filename;

Lib:
	name=LibQN;

LibsDef:
	'$_LIBS' Assignment (libs+=Lib)+;

ModQN:
	QualifiedName;

Mod:
	name=ModQN;

ModsDef:
	('$_MODS' | '$_MODS_CORE') Assignment (mods+=Mod)+;

Prop:
	DepsProp | SrcsProp | ProvidesProp | RequiresProp | DetailsProp | SubsProp | UsesProp | FlagsProp | BriefProp;

PropAssign:
	DepsPropAssign | SrcsPropAssign | ProvidesPropAssign | RequiresPropAssign | SubsPropAssign | UsesPropAssign |
	FlagsPropAssign | BriefPropAssign | DetailsPropAssign;

DepsProp:
	'$_DEPS-' subject=[Mod|ModQN];

DepsPropAssign:
	prop=DepsProp Assignment (ContLine? mods+=[Mod|ModQN])+;

SrcsProp:
	'$_SRCS-' subject=[Lib|LibQN] |
	'$_SRCS-' subject=[Mod|ModQN];

SrcsPropAssign:
	prop=SrcsProp Assignment (ContLine? filenames+=FilenameWithWildcards)+;

UsesProp:
	'$_USES-' subject=[Lib|LibQN] |
	'$_USES-' subject=[Mod|ModQN];

UsesPropAssign:
	prop=UsesProp Assignment (ContLine? libs+=[Lib|LibQN])+;

SubsProp:
	'$_SUBS-' subject=[Lib|LibQN];

SubsPropAssign:
	prop=SubsProp Assignment (ContLine? libs+=[Lib|LibQN])+;

ProvidesProp:
	'$_PROVIDES-' subject=[Mod|ModQN];

ProvidesPropAssign:
	prop=ProvidesProp Assignment (ContLine? apis+=[Api|ApiQN])+;

RequiresProp:
	'$_REQUIRES-' subject=[Mod|ModQN];

RequiresPropAssign:
	prop=RequiresProp Assignment (ContLine? apis+=[Api|ApiQN])+;

BriefProp:
	'$_BRIEF-' subject=[Mod|ModQN];

BriefPropAssign:
	prop=BriefProp Assignment Text |
	'define' prop=BriefProp
	Text
	'endef';

DetailsProp:
	'$_DETAILS-' subject=[Mod|ModQN];

DetailsPropAssign:
	prop=DetailsProp Assignment Text |
	'define' prop=DetailsProp
	Text
	'endef';

Subdir:
	'SUBDIRS' Assignment (MakeText)*;

Flags:
	('$_LDFLAGS' | '$_CPPFLAGS' | '$_CFLAGS') Assignment MakeText?;

FlagsProp:
	('$_CPPFLAGS-' | '$_CFLAGS-') subject=[Lib|LibQN] |
	('$_CPPFLAGS-' | '$_CFLAGS-') subject=[Mod|ModQN];

FlagsPropAssign:
	prop=FlagsProp Assignment MakeText?;

QualifiedName:
	ID ('.' ID)*;

Assignment:
	':=' | '+=' | '=';