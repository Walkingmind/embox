grammar org.embuild.emdsl.EmDsl with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate emDsl "http://www.embuild.org/emdsl/EmDsl"

Model:
	Package?
	(entities+=Entity)*;

terminal ML_COMMENT:
	'#/##'->'##/#';

terminal SL_COMMENT:
	'#' !('\n' | '\r')* ('\r'? '\n')?;

FILENAME:
	ID | '-' | '.';

WILDCARD returns ecore::EString:
	(FILENAME
	| '?' | '*'
	| '[' FILENAME
	('|' FILENAME)* ']')+;

OTHER:
	('_' | '$' | '/' | ',' | '\\' | '(' | ')' | WILDCARD)+;

Entity:
	Subdir | Module | Flag | Api;

Package:
	'$_PACKAGE' Assignment name=QualifiedName;

Subdir:
	'SUBDIRS' Assignment OTHER;

FlagModifier:
	'$_LDFLAGS' | '$_CPPFLAGS';

Flag:
	FlagModifier Assignment flagname=OTHER;

Api:
	'$_APIS' Assignment name=QualifiedName;

ModuleModifier:
	'$_MODS' | '$MODS_CORE';

Module:
	ModuleModifier Assignment name=ID
	(properties+=Property)*;

Property:
	ModsProperty | SrcProperty | ApiProperty;

PropertyModifier:
	'$_REQUIRES-' | '$_DEPS-';

ModsProperty:
	PropertyModifier (subject=[Module|QualifiedName]) Assignment module=[Module|QualifiedName];

SrcProperty:
	'$_SRCS-' (subject=[Module|QualifiedName]) Assignment filename=WILDCARD;

ApiProperty:
	'$_PROVIDES-' (subject=[Module|QualifiedName]) Assignment api=[Api|QualifiedName];

QualifiedName:
	ID ('.' ID)*;

Assignment:
	':=' | '+=' | '=';